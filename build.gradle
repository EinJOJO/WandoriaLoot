plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.einjojo'
version = '0.9.3-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "jojo-repo"
        url = "https://mvn.einjojo.de/snapshots"
    }
    maven {
        name = "dmulloy2-repo"
        url "https://repo.dmulloy2.net/repository/public/"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation("me.einjojo:JosLibrary:0.4-SNAPSHOT")
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.9'
    compileOnly group: "org.spigotmc", name:"spigot", version:"1.19.2-R0.1-SNAPSHOT"
    compileOnly group: "com.comphenix.protocol", name:"ProtocolLib", version: "5.0.0-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion

    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

assemble{
    dependsOn(shadowJar)
}

shadowJar {
    minimize()
    archiveFileName.set("${rootProject.name}-${project.version}.jar")
    relocate 'com.github.stefvanschie.inventoryframework', 'me.einjojo.wandorialoot.util.inventoryframework'
}


tasks.register('moveToServer') {
    doLast {
        delete fileTree(dir: 'D:\\workspace\\minecraft\\testserver\\zwandoria\\plugins', include: "${rootProject.name}*.jar")
        copy {
            from "${buildDir}/libs/${rootProject.name}-${project.version}.jar"
            into "D:\\workspace\\minecraft\\testserver\\zwandoria\\plugins"
        }
    }
}
build {
    finalizedBy(moveToServer)
}